Programação para ProfitChart - Plataforma de operações no mercado financeiro

Linguagem utilizada: Nelogica Trading System Language (EasyLanguage)


Setup: Larry Williams - 9.2 (long e short, respectivamente)

Definição: Com a média exponencial de 9 períodos ascendente, espera-se que um candle feche abaixo da mínima do candle anterior. Isto ocorrendo,
compra no rompimento da máxima do candle que fechou abaixo.

*************************** INICIO DO CÓDIGO (long) ***************************
var
  compra,venda,stoploss : Float;
  media9                : boolean;
Inicio
  media9 := (MediaExp(9,Close) > MediaExp(9,Close)[1]) e (MediaExp(9,Close)[1] > MediaExp(9,Close)[2]) e (MediaExp(9,Close)[2] > MediaExp(9,Close)[3]) e (MediaExp(9,Close)[3] > MediaExp(9,Close)[4]);
  Se (IsBought) então
    Inicio
      //parametro stop
      venda := minima[1] - 0.01;
      Se (MediaExp(9,Close) < MediaExp(9,Close)[1]) e (mediaexp(9,close)[1] < mediaexp(9,close)[2]) então
        SellToCoverStop(venda,venda);
      Se (MediaExp(9,Close) < MediaExp(9,Close)[1]) e (mediaexp(9,close)[1] < mediaexp(9,close)[2]) e (abertura < minima[1]) então
        SellToCoverStop(abertura,abertura);
      Selltocoverstop(stoploss,stoploss);
    Fim
  Senão 
    Inicio
      stoploss := minima[1] - 0.01;
      //compra com 1 candle de recuo
      Se (media9) e (fechamento[2] > fechamento[3]) e (fechamento[2] > fechamento[1]) e (Fechamento[1] < minima[2]) 
      então BuyStop(compra,compra) //candle referencia: [2]
      //compra com 2 candles de recuo
      Senao Se (media9) e (fechamento[3] > fechamento[4]) e (fechamento[3] > fechamento[2]) e (Fechamento[2] < minima[3]) e (Maxima[1] < Maxima[2]) 
      então BuyStop(compra,compra) //candle referencia: [3]
      //compra com 3 candles de recuo
      Senao se (media9) e (fechamento[4] > fechamento[5]) e (fechamento[4] > fechamento[3]) e (Fechamento[3] < Minima[4]) e (Maxima[2] < Maxima[3]) e (Maxima[1] < Maxima[2]) 
      então buystop(compra,compra); //candle referencia: [4]
      //parametros start
      compra := maxima + 0.01;
      
    Fim;
Fim;


*************************** INICIO DO CÓDIGO (short) ***************************
var
  compra,venda,stoploss : Float;
  media9                : boolean;
Inicio
  media9 := (MediaExp(9,Close) < MediaExp(9,Close)[1]) e (MediaExp(9,Close)[1] < MediaExp(9,Close)[2]) e (MediaExp(9,Close)[2] < MediaExp(9,Close)[3]) e (MediaExp(9,Close)[3] < MediaExp(9,Close)[4]);
  Se (IsSold) então
    Inicio
      //parametro stop
      compra := maxima[1] + 0.01;
      Se (MediaExp(9,Close) > MediaExp(9,Close)[1]) e (mediaexp(9,close)[1] > mediaexp(9,close)[2]) então
        BuyToCoverStop(compra,compra);
      Se (MediaExp(9,Close) > MediaExp(9,Close)[1]) e (mediaexp(9,close)[1] > mediaexp(9,close)[2]) e (abertura > Maxima[1]) então
        BuyToCoverStop(abertura,abertura);
      BuyToCoverStop(stoploss,stoploss);
    Fim
  Senão 
    Inicio
      //venda com 1 candle de recuo
      Se (media9) e (fechamento[2] < fechamento[3]) e (fechamento[2] < fechamento[1]) e (Fechamento[1] > maxima[2]) 
      então SellShortStop(venda,venda) //candle referencia: [2]
      //venda com 2 candles de recuo
      Senao Se (media9) e (fechamento[3] < fechamento[4]) e (fechamento[3] < fechamento[2]) e (Fechamento[2] > maxima[3]) e (Minima[1] > Minima[2]) 
      então SellShortStop(venda,venda) //candle referencia: [3]
      //venda com 3 candles de recuo
      Senao se (media9) e (fechamento[4] < fechamento[5]) e (fechamento[4] < fechamento[3]) e (Fechamento[3] > maxima[4]) e (Minima[2] > Minima[3]) e (Minima[1] > Minima[2]) 
      então SellShortStop(venda,venda); //candle referencia: [4]
      //parametros start
      venda := minima - 0.01;
      stoploss := maxima[1] + 0.01;
    Fim;
Fim;